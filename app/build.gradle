apply plugin: 'com.android.application'
if (getGradle().getStartParameter().getTaskRequests().toString().contains("standard")) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.gianlu.aria2android"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 34
        versionName "1.9.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'
        }

        foss {
            dimension 'main'
        }
    }
}

dependencies {
    api project(':LovelyMaterialPreferences')

    standardApi(project(':CommonUtils')) {
        exclude group: 'com.getkeepsafe.taptargetview'
    }

    fossApi(project(':CommonUtils')) {
        exclude group: 'com.android.billingclient'
        exclude group: 'com.google.firebase'
        exclude group: 'com.crashlytics.sdk.android'
        exclude group: 'com.getkeepsafe.taptargetview'
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("standard")) {
    apply plugin: 'com.google.gms.google-services'
}